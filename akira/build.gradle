plugins {
    id 'java'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.jib)
}

group = 'org.azurecloud.solutions'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
    }
}

dependencies {
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.starter.security
    implementation libs.liquibase.core
    implementation libs.telegrambots.spring.boot.starter
    implementation libs.httpclient

    runtimeOnly libs.postgresql

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.register('npmInstall', Exec) {
    workingDir '../akira-frontend'
    commandLine System.getProperty("os.name").startsWith("Windows") ? 'npm.cmd' : 'npm', 'install'
}

tasks.register('buildFrontend', Exec) {
    dependsOn npmInstall
    workingDir '../akira-frontend'
    commandLine System.getProperty("os.name").startsWith("Windows") ? 'npm.cmd' : 'npm', 'run', 'build'
}

// Make the 'processResources' task depend on 'buildFrontend'
processResources.dependsOn buildFrontend

sourceSets {
    main {
        resources {
            srcDirs "../akira-frontend/dist/akira-frontend/browser"
        }
    }
}

processResources {
    from("../akira-frontend/dist/akira-frontend/browser") {
        into "static"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

jib {
    from {
        image = 'eclipse-temurin:24-jre-alpine'
    }
    to {
        def dockerUsername = project.findProperty('docker.username') ?: System.getenv('DOCKER_USERNAME') ?: 'akira'
        def imageName = project.findProperty('docker.image') ?: 'akira'
        image = "${dockerUsername}/${imageName}"
        tags = [project.version.toString(), 'latest']
        auth {
            username = project.findProperty('docker.username') ? System.getenv('DOCKER_USERNAME') : ""
            password = project.findProperty('docker.password') ? System.getenv('DOCKER_PASSWORD') : ""
        }
    }
    container {
        jvmFlags = ['-Djava.security.egd=file:/dev/./urandom']
        ports = ['8443']
        user = 'spring:spring'
        workingDirectory = '/app'
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}
