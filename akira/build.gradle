plugins {
    id 'java'
    alias(libs.plugins.spring.boot)
    alias(libs.plugins.spring.dependency.management)
    alias(libs.plugins.jib)
}

group = 'org.azurecloud.solutions'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.java.get())
    }
}

dependencies {
    implementation libs.spring.boot.starter.web
    implementation libs.spring.boot.starter.data.jpa
    implementation libs.spring.boot.starter.security
    implementation libs.liquibase.core
    implementation libs.telegrambots.spring.boot.starter
    implementation libs.httpclient

    runtimeOnly libs.postgresql

    compileOnly libs.lombok
    annotationProcessor libs.lombok
    
    testImplementation libs.spring.boot.starter.test
    testImplementation libs.spring.security.test
}

tasks.register('npmInstall', Exec) {
    workingDir '../akira-frontend'
    commandLine System.getProperty("os.name").startsWith("Windows") ? 'npm.cmd' : 'npm', 'install'
}

tasks.register('buildFrontend', Exec) {
    dependsOn npmInstall
    workingDir '../akira-frontend'
    commandLine System.getProperty("os.name").startsWith("Windows") ? 'npm.cmd' : 'npm', 'run', 'build'
}

// Make the 'processResources' task depend on 'buildFrontend'
processResources.dependsOn buildFrontend

sourceSets {
    main {
        resources {
            srcDirs "../akira-frontend/dist/akira-frontend/browser"
        }
    }
}

processResources {
    from("../akira-frontend/dist/akira-frontend/browser") {
        into "static"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

def baseImage = 'bellsoft/liberica-openjre-alpine:24'
def dockerRegistry = providers.gradleProperty('docker.username').getOrElse('akira')
def imageTag = project.version.toString()
def fullVersion = project.version.toString()

jib {
    from {
        image = "${baseImage}"
    }
    to {
        image = "${dockerRegistry}/akira"
        tags = [imageTag, 'latest']
        auth {
            username = providers.gradleProperty('docker.username')
                    .orElse(providers.environmentVariable('DOCKER_USERNAME'))
                    .getOrElse('akira')
            password = providers.gradleProperty('docker.password')
                    .orElse(providers.environmentVariable('DOCKER_PASSWORD'))
                    .getOrElse('')
        }
    }
    container {
        labels.put 'org.opencontainers.image.version', "${fullVersion}"
        labels.put 'org.opencontainers.image.title', 'Akira'
        labels.put 'org.opencontainers.image.description', 'Akira Project'
        
        jvmFlags = ['-Djava.security.egd=file:/dev/./urandom']
        ports = ['8443']
        workingDirectory = '/app'
        creationTime = "USE_CURRENT_TIMESTAMP"
        mainClass = 'org.azurecloud.solutions.akira.AkiraApplication'
    }
}
